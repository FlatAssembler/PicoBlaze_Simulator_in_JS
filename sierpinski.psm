address 0

base_decimal

constant width_of_the_triangle, 50

print_string "We are about to print the Sierpinski Triangle into the UART terminal...", s9, UART_TX
load s9, a'x
call UART_TX

load s0, 0
setup_loop:
  compare s0, width_of_the_triangle
  jump nc, end_of_the_setup_loop
  compare s0, width_of_the_triangle / 2
  jump z, we_are_in_the_middle
  jump nz, we_are_not_in_the_middle
  we_are_in_the_middle:
    load s1, 1
    store s1, (s0)
    load s9, "*"
    call UART_TX
    jump end_of_the_branching_in_setup
  we_are_not_in_the_middle:
    load s1, 0
    store s1, (s0)
    load s9, " "
    call UART_TX
    jump end_of_the_branching_in_setup
  end_of_the_branching_in_setup:
  add s0, 1
  jump setup_loop
end_of_the_setup_loop:
load s9, a'x
call UART_TX

load s3, 1
printing_loop:
  compare s3, width_of_the_triangle
  jump z, end_of_the_printing_loop
  
  load s9, " "
  call UART_TX

  load s0, 0
  load s1, 2
  load s2, width_of_the_triangle + 1
  next_iteration_loop:
    compare s2, 2 * width_of_the_triangle
    jump z, end_of_the_next_iteration_loop

    fetch s4, (s0)
    fetch s5, (s1)
    xor s4, s5
    test s4, s4
    jump z, we_are_printing_a_zero
    jump nz, we_are_printing_a_one
    we_are_printing_a_zero:
      store s4, (s2)
      load s9, " "
      call UART_TX
      jump end_of_the_branching_in_the_next_iteration_loop
    we_are_printing_a_one:
      store s4, (s2)
      load s9, "*"
      call UART_TX
      jump end_of_the_branching_in_the_next_iteration_loop
    end_of_the_branching_in_the_next_iteration_loop:

    add s2, 1
    add s1, 1
    add s0, 1
    jump next_iteration_loop
  end_of_the_next_iteration_loop:
  
  load s9, a'x
  call UART_TX

  load s0, 0
  load s1, width_of_the_triangle
  copying_loop:
    compare s0, width_of_the_triangle
    jump z, end_of_the_copying_loop

    fetch s2, (s1)
    store s2, (s0)

    add s0, 1
    add s1, 1
    jump copying_loop
  end_of_the_copying_loop:

  add s3, 1
  jump printing_loop
end_of_the_printing_loop:

print_string "The end.", s9, UART_TX
load s9, a'x
call UART_TX

infinite_loop: jump infinite_loop

base_hexadecimal

CONSTANT LED_PORT,         00
CONSTANT HEX1_PORT,        01
CONSTANT HEX2_PORT,        02
CONSTANT UART_TX_PORT,     03
CONSTANT UART_RESET_PORT,  04
CONSTANT SW_PORT,          00
CONSTANT BTN_PORT,         01
CONSTANT UART_STATUS_PORT, 02
CONSTANT UART_RX_PORT,     03
; Tx data_present
CONSTANT U_TX_D, 00000001'b
; Tx FIFO half_full
CONSTANT U_TX_H, 00000010'b
; TxFIFO full
CONSTANT U_TX_F, 00000100'b
; Rxdata_present
CONSTANT U_RX_D, 00001000'b
; RxFIFO half_full
CONSTANT U_RX_H, 00010000'b
; RxFIFO full
CONSTANT U_RX_F, 00100000'b

UART_RX:
  INPUT sA, UART_STATUS_PORT
  TEST  sA, U_RX_D
  JUMP  NZ, input_not_empty
    LOAD  s0, s0
    JUMP UART_RX
  input_not_empty:
  INPUT s9, UART_RX_PORT
RETURN

UART_TX:
  INPUT  sA, UART_STATUS_PORT
  TEST   sA, U_TX_F
  JUMP   NZ, UART_TX
  OUTPUT s9, UART_TX_PORT
RETURN

